apply plugin: 'com.github.dcendents.android-maven'
def EMPTY = 'unspecified'
def NULL = null
group = publishedGroupId

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }

                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }.withXml {
            def containers = [configurations.api, configurations.implementation]
            def dependenciesNode = asNode().appendNode('dependencies')
            containers.each { container ->
                container.dependencies.each { dependency ->
                    //通过本地引用的不要打进去
                    if (dependency.group == NULL || dependency.group == EMPTY) {
                        return
                    }
                    if (dependency.name == NULL || dependency.name == EMPTY) {
                        return
                    }
                    if (dependency.version == NULL || dependency.version == EMPTY) {
                        return
                    }
                    if (dependency.group.startsWith("android") || dependency.group.startsWith("androidx")) {
                        //android 的东西不打进去
                        return
                    }
                    println("pom 清单包含：   ${dependency.group}:${dependency.name}:${dependency.version}")
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                    dependencyNode.appendNode('scope', container.name)
                }
            }
        }
    }
}